/*************************************************************************************************\
* Наименование: Ведение телефонного справочника                                                   *
* Контур/Модуль: Кадры                                                                            *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *

\*************************************************************************************************/

const
end;
#include Query.vih
#include objintpl.vin
#include runalg_viewAnalPos.vih
#include ProtPlan_CF.vih

#component "M_MnPlan"
Interface runalg_viewAnalPos 'Просмотр аналитики по позиции';

create view
var
    _formPL
  , _cSpMnPl
  , _cMnPlan
  , _cSpMnPlan
  , _CIZG
  , _CPOTR : comp;
    _guid  : string;
    _tPlan : word;
   _TotalKolStr : string;
as select
 mnplan.nrec
from
   mnplan
 , formPL
 , SpMnPlan
 , spmnpl
 , spmnpl spmnpl_1
 , valspmnp
 , bufspplam
 , katrole katrole2
 , katrole katrole3
 , katparty vidprod
 , spkau model
 where ((
              _cMnPlan == MnPlan.nrec
           and _formPL == formpl.nrec

           and _cSpMnPlan == SpMnPlan.nrec
        and spmnplan.nrec == spmnpl.cspmnplan

        and SpMnPlan.cizd == katmc.nrec
      and SpMnPlan.cotped == katotped.nrec
            and katmc.ced == kated.nrec

    and spmnplan.crolean2 == katrole2.nrec
    and spmnplan.crolean3 == katrole3.nrec

            and _guid   == bufspplam.sras
            and _tPlan  == bufspplam.tPlan (noindex)
      and SpMnPlan.cizd == bufspplam.CMC   (noindex)
      and         _CIZG == bufspplam.CIZG  (noindex)
      and        _CPOTR == bufspplam.CPOTR (noindex)
   and spmnpl.startdate == bufspplam.DT (noindex)

      and bufspplam.vidprod == vidprod.nrec
      and bufspplam.model   == model.nrec

       ))
bounds bydate _cSpMnPl == spmnpl.nrec (noindex)
;

//#include ShowKau.vpp  // Макроопределения для высветки КАУ, выбора КАУ

function getkolbyspmnpl( __cSpMnPl : comp) : double; cacheable;{
  result := 0;
  _loop valspmnp where ((__cSpMnPl == valspmnp.cspmnpl)) {
    result += valspmnp.kol;
  }
}
function getTotalkol : double; {
  result := 0;
  _loop spmnpl {
    result += getkolbyspmnpl(spmnpl.nrec);
  }
}
procedure setTotalKolStr; {
  set _TotalKolStr := '0';
  var _formatDouble : string = '[|-]3666 666 666 666 666.88';
  var _totalkol : double = 0;
  _totalkol := getTotalkol;

  if round(_Totalkol, 0) = _totalkol
       then _formatDouble := '[|-]3666 666 666 666 666';
  set _TotalKolStr := DoubleToStr(_totalkol, _formatDouble);
//  message('_TotalKolStr = ' + _TotalKolStr);
}

window winViewAnalPosAll 'Расшифровка аналитики по позиции';
show at (,,50,20);
Screen scrwinViewAnalPos;
  show at (,,, 5);
 table spmnplan;
  Fields
    katmc.name : skip;
    if(isvalidall(tnkatotped),katotped.name,kated.name) : skip;
    Katrole2.name : skip;
    ShowKau(spmnplan.WKODGR2, spmnplan.CANVAL2) : skip;
    Katrole3.name : skip;
    ShowKau(spmnplan.WKODGR3, spmnplan.CANVAL3) : skip;
    _TotalkolStr : skip,{Font = {Bold = True}};
<<'Просмотр информации по позиции'
.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 ед. изм.@@@@@@@@@@@
.@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@
.@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@
Общее количество .@@@@@@@@@@@@@@@
>>
end;
formatsGroup grViewAll;
  Show at (,6,,);//  alwaysShowhScroll ;//excelColumns, ,manualHScroll
 browse brSpmnpl ('',,sci1Esc);
// show at(,2,30,10);
   show at(,6,25,);
 table spmnpl;
 fields
   spmnpl.startdate            'Дата'  headerAlign = centered:protect,[4];
   getkolbyspmnpl(spmnpl.nrec) 'кол-во' headerAlign = centered:protect,[4, '\2p[|-]36`666`666`666`666.88'];
 end;
 Browse brbufspplam ('',,sci1Esc);
  Show at (26,6,,);//  alwaysShowhScroll ;//excelColumns, ,manualHScroll
  Table bufspplam;
  Fields
   vidprod.name   'Вид продукции' headerAlign = centered:protect,[4];
   model.name     'Модель'        headerAlign = centered:protect,[4];
   bufspplam.kol  'Количество'    headerAlign = centered:protect,[4, '\2p[|-]36`666`666`666`666.88'];
 end;
end;
formatsGroup grViewOne
  Show at (,6,,);//  alwaysShowhScroll ;//excelColumns, ,manualHScroll
 Browse brbufspplamone ('',,sci1Esc);
  Show at (,6,,);//  alwaysShowhScroll ;//excelColumns, ,manualHScroll
  Table bufspplam;
  Fields
   spmnpl.startdate            'Дата'  headerAlign = centered:protect,[4];
   vidprod.name   'Вид продукции' headerAlign = centered:protect,[4];
   model.name     'Модель'        headerAlign = centered:protect,[4];
   bufspplam.kol  'Количество'    headerAlign = centered:protect,[4, '\2p[|-]36`666`666`666`666.88'];
 end;
end;
end;
windowevent winViewAnalPosAll;
cminit: {
 AddBounds(tbbydate);
 setformat(grViewOne);
 setTotalKolStr;
 rereadrecord;

}
cmHotKeys : {
     PutHotCommand(runMenu('mnu_change_view'));
}
cmvalue1: {
  if BoundActive(tbbydate) {
   SubBounds(tbbydate);
   setformat(grViewAll);
  } else {
   AddBounds(tbbydate);
   setformat(grViewOne);
  }
 setTotalKolStr;
 rescanpanel(tnspmnplan);
  rescanpanel(tnspmnpl);
 if getfirst spmnpl where ((_cSpMnPl == spmnpl.nrec)) = tsOK {}
 rereadrecord;
}
end;

procedure Run(__cMnPlan : comp; __formPL : comp; __cSpMnPl : comp); {

  set _cMnPlan := __cMnPlan;
  if getfirst mnplan <> tsOK {
    message('Не найден mnplan по nrec = ' + string(_cMnPlan,0,0), error);
    exit;
  }

  set _guid := sgetattr(coMNplan, _cMnPlan, 'GUID_bufspplam');
  if _guid = '' {
    message('У плана пустой атрибут "GUID_bufspplam"', error);
    exit;
  }

 if getfirst spmnpl_1 where ((__cSpMnPl == spmnpl_1.nrec)) <> tsOK {
    message('Не найден spmnpl по nrec = ' + string(_cSpMnPl,0,0), error);
    exit;
 }
 set _cSpMnPl := __cSpMnPl;

 set _cSpMnPlan := spmnpl_1.cspmnplan;
 if getfirst spmnplan <> tsOK {
    message('Не найден spmnplan по nrec = ' + string(_cSpMnPlan,0,0), error);
    exit;
 }

  set _tPlan := 0;
  case MnPlan.cWayAccou of
   co_PotrMat_VnutrProizv : { // Внутреннее производство деталей - потребность в материалах
     set _tPlan := 1;
     set _CPOTR := spmnplan.CANVAL2;
     set _CIZG  := spmnplan.CANVAL3;
    }
   co_PLanSnab_PotrMat    : { // Потребность в материалах - планы снабжения
     set _tPlan := 2;
     set _CIZG  := spmnplan.CANVAL2;
     set _CPOTR := spmnplan.CANVAL3;
    }
   co_PLanSnab_VneshPost  : { // Внешняя поставка деталей - планы снабжения
     set _tPlan := 3;
     set _CIZG  := spmnplan.CANVAL2;
     set _CPOTR := spmnplan.CANVAL3;
    }
  end;

  if _tPlan = 0 {
    message('MnPlan.cWayAccou = ' + string(MnPlan.cWayAccou,0,0) + ' не найден среди констант шаблонов для работы', error);
    exit;
  }

  if getfirst bufspplam <> tsOK {
    message('Нет ни одной записи в BUFSPPLAM с SRAS = "' + _guid+'"'+
    ''#13'' + '  tPlan = "' + string(_tPlan) +'"'+
    ''#13'' + '    CMC = "'  + string(SpMnPlan.cizd,0,0)+'"'+
    ''#13'' + '  CIZG  = "' + string(_CIZG) +'"'+
    ''#13'' + '  CPOTR = "' + string(_CPOTR) +'"', error);
    exit;
  }

  set _formPL := __formPL;
  if getfirst formpl <> tsOK {
    message('Не найден formpl по nrec = ' + string(_formPL,0,0), error);
    exit;
  }

  runwindowmodal(winViewAnalPosAll);

  if VisualizationActive then stopvisual;
  //ProcessText(_logFile, vfEscable or vfNewTitle or vfMacroSize, 'Протокол');

}
end.
mnu_change_view Menu {
 - 'Просмотр даты все/текущая позиция', cmValue1,'Просмотр количества по всем датам или только по той, с которой запустили',,'',,sci1Esc;
}
